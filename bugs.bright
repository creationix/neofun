length = 60 // Our strip has 60 LEDs
bpp = 3     // We are using normal ws2812b lights that expect 3 bytes of color.
delay = 33  // Delay between animation loops in ms (defaults to 100)
sprites = 4 // Total number of sprites to spawn at startup
fizz = 3    // How many sprites to update per animation loop (defaults to all)

// This is called once at program startup
setup {
  d = 0
}
// This is called once per animation loop before updating sprites.
loop {
  fade(d)
  d = (d % 4) + 1
}

// This is called for each sprite when it's spawned.
// Variables created here are local to the sprite.
spawn {
  p = rand(length)    // Random starting position
  h = rand(768)       // Random starting hue
  l = rand(50) + 50   // Semi-random lifetime
  r = rand(2) * 2 - 1 // Random direction
}

// This is called once per sprite update
// you can access global variables as well as sprite local variables.
update {
  p:length += 1  // Move 1 pixel forward
  h:768 += 7     // Slightly change hue
  hue(p, h, 100) // Draw new position
  l -= 1         // Decrement lifetime.
  if l <= 0 {
    explode()    // When lifetime reaches zero, explode!
  }
}

// draw gradient that color shifts while fading away.
explode {
  b = 255
  loop i:14 {
    b -= 18
    hue(p + i, h, b)
    hue(p - i, h, b)
  }
  respawn() // And then restart the sprite.
}
